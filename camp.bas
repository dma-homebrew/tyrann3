10 REM {+ ORIC 2014 + TYRANN 3 + MAXIMUS & HIALMAR +}
20 GOSUB 48000: REM AD=#BFE0:X=PEEK(AD):Y=PEEK(AD+1):S=PEEK(AD+2)
40 REM DIM C(34,24)
50 T1=0
60 POKE 48035,0:POKE#26A,PEEK(#26A) AND 254
80 GOSUB 50000:PING
85 FORI=1TO4:CL(1,I)=0:NEXT
90 GOTO 21000

200 REM Centrage
210 T=INT((42-LEN(S$))/2):PRINT @T,L;S$
220 RETURN

299 REM LABY
300 GOSUB 49000 : LOAD"LABY"

4400 S$="INSPECTER QUEL HEROS ? ":GOSUB 5650:PRINT@6,3;S$
4410 GOSUB 30200:P=A
4420 S$=N$(P)+" ":IF MP(P)<>8 THEN S$=S$+M$(MP(P))
4430 L=23:CLS:GOSUB 10000
4440 PRINT @4,3;"Carr :";C$(CP(P)):PRINT @21,3;"Niv:"NI(P);"EXP:"XP(P)
4450 PRINT @4,5;"Sante:";OK$(OK(P)):PRINT @21,5;"PV :";ET(P)"/"PV(P)
4460 PRINT @4,7;"Bourse:";RI(P)"Cerfs d'Argent"
4470 PRINT @4,9;"Arme D: ";IT$(WR(P))
4480 PRINT @4,10;"Arme G: ";IT$(WL(P))
4490 PRINT @4,11;"Animal: ";IT$(BT(P))
4500 PRINT @4,12;"Armure: ";IT$(PT(P));"  CA:";CA(P)
4510 S$="CC  CT  Fo  Ag  In  FM "
4520 PRINT@4,14;CHR$(145)CHR$(135)S$CHR$(144)
4530 S$=STR$(CC(P))+" "+STR$(CT(P))+" "+STR$(FO(P))+" "+STR$(AG(P))+" "+STR$(IN(P))+" "+STR$(FM(P))
4540 PLOT 5,15,S$
4550 L=16:OP(P)=0
4560 FOR I=1TO6
4570 IF SAD(P,I)>0 THEN M$=ITEM$(SAD(P,I)):OP(P)=OP(P)+1 ELSE M$=".............."
4580 PRINT @3,L+I;I;" ";M$:NEXT I
4590 PRINT @29,17;"Passes:":PRINT @31,19;STR$(NP)
4600 S$=" DONNER: ":GOSUB 5600:PRINT @1,24;S$
4610 S$=" A)rgent   O)bjet ?  ":GOSUB 5650:PRINT @14,24;S$
4620 GET A$:IF A$<>"A" AND A$<>"O" AND A$<>" " THEN 4620
4630 IF A$<>" " THEN 4650
4640 IF CP(P)>3 THEN GOSUB 4700:GOTO 4660 ELSE 4660
4650 IF A$="A" THEN GOSUB 5700 ELSE GOSUB 5800
4660 RETURN

4700 CLS:S$=" * SORTS * ":L=14:GOSUB 10000
4710 FOR I=1TO NI(P)
4720 S$=STR$(I)+" - "+SPELL$(CP(P)-3,I):PRINT@11,I+3;S$
4730 S$="("+STR$(SN(P,I))+" )": PRINT@25,I+3;S$:NEXT
4735 IF CP(P)<>5 THEN GOSUB 30000:GOTO 4900
4740 S$=" Un sort de soin (O/N)?":L=13:GOSUB 200
4750 GETA$:IF A$="N" THEN 4900 ELSE IF A$<>"O" THEN 4750
4760 GOSUB 10800:S$="        LEQUEL  ?      ":L=13:GOSUB 200
4770 GETA$:SP=VAL(A$):IFSP<1 OR SP=4 OR SP=6 OR SP>7 THEN ZAP:GOTO 4770
4775 S$=" INCANTATION:"+SP$(2,SP)+" ":L=13:GOSUB 200
4780 IF SP=5 THEN 4870
4790 S$="     SOIGNER QUI  ?    ":L=17:GOSUB 200
4800 GOSUB 30200:P=A
4810 IF (SP=1 AND OK(P)=4) OR (SP=2 AND OK(P)<>2) OR (SP=3 AND OK(P)<>3) OR (SP=7 AND OK(P)<>4) THEN ZAP:GOTO 4900
4840 ET(P)=PV(P):IF SP<>1 THEN OK(P)=1
4850 GOTO 4900
4870 FOR I=1TO6:OK(I)=1:ET(I)=PV(I):NEXT
4900 RETURN

4990 REM SOINS

5000 S$="SOIGNER QUEL HEROS ? ":GOSUB 5650:PRINT@9,3;S$
5020 GETP$:P=VAL(P$):IF P<1 OR P>6 THEN PING:GOTO 5020
5040 ENC=2:S$="LE MESTRE LUWIN":L=16:CLS:GOSUB 10000
5050 PRINT @5,5;"Bienvenue ";N$(P)"
5060 PRINT @5,7;"Votre condition est: ";OK$(OK(P))
5070 S$="Etat:"+STR$(ET(P))+" /"+STR$(PV(P)):PLOT 5,9,S$
5080 IF OK(P)=1 AND ET(P)=PV(P) THEN M$="Vous etes en pleine forme":HO=0
5090 IF OK(P)=2 OR OK(P)=3 THEN HO=NI(P)*50
5100 IF OK(P)=4 THEN HO=NI(P)*100
5110 IF OK(P)=1 AND ET(P)<PV(P) THEN HO=NI(P)*25
5120 IF HO=0 THEN PLOT 7,11,M$:PING:WAIT TI*7:GOTO 4000
5130 PRINT @5,11;"HONORAIRES: ";HO;" ca"
5140 PLOT 5,13,"JE VOUS SOIGNE (O/N) ?"
5150 GETA$
5160IF A$="N" THEN 4000
5170 IF A$="O" THEN 5190
5180 GOTO 5150
5190 IF HO > RI(P) THEN S$="Par les 7, Vous etes trop pauvres !":GOTO 5210
5200 RI(P)=RI(P)-HO:OK(P)=1:ET(P)=PV(P):HO=0:S$="Par les 7, Vous voila gueri !"
5210 PING:GOSUB 5650:PRINT@3,14;S$
5250 WAIT25*TI:GOTO 4000

5300 REM AFFICHE EQUIPE:REM  NEW +++++++
5310 CLS:PRINT@8,1;CHR$(145)CHR$(128)" * TYRANN 3 - EQUIPE *  "CHR$(144)
5312 L=3:PRINT@3,L;CHR$(145)"PERSONNAGES  MAISON  CARRIERE  NIV "CHR$(144)
5315 PRINT@3,L+1;CHR$(148)" Argent      CC CT Fo Ag In FM PV  "CHR$(144):L=5
5320 FOR I=1TO6
5330  IF CP(I)=1 THEN ENC=131
5340  IF CP(I)=2 THEN ENC=135
5350  IF CP(I)=3 THEN ENC=134
5360  IF CP(I)=4 THEN ENC=133
5370  IF CP(I)=5 THEN ENC=132
5380  IF CP(I)=6 THEN ENC=130
5390 PRINT @ 1,L;CHR$(ENC);I;N$(I);:PRINT @ 17,L;M$(MP(I)):PRINT @ 27,L;C$(CP(I))
5395 PRINT @ 37,L;NI(I)
5400 PRINT @ 4,L+1;STR$(RI(I));" ca"
5410 S$=STR$(CC(I))+STR$(CT(I))+STR$(FO(I))+STR$(AG(I))+STR$(IN(I))+STR$(FM(I))+STR$(PV(I))
5420 PRINT@16,L+1;S$:L=L+3
5430 NEXT I:PRINT@3,L-1;CHR$(148)" Argent      CC CT Fo Ag In FM PV  "CHR$(144):WAIT300
5435 PRINT@3,L;CHR$(145)CHR$(128)"            < ESPACE >            "CHR$(144)
5440 ENC=4
5450 GET A$:IF A$<>" " THEN 5450
5460 RETURN

5600 REM FOND BLEU 
5610 S$=" "+CHR$(148)+CHR$(128)+S$+CHR$(134)+CHR$(144)+" "
5620 RETURN

5650 REM FOND ROUGE
5660 S$=" "+CHR$(145)+CHR$(135)+S$+CHR$(128+EE(VIL))+CHR$(144)+" "
5670 RETURN

5699 REM DONNER
5700 L=18:CLS:S$=N$(P)+" DONNE":GOSUB 10000
5710 FORI=1TO6:PRINT @4,3+I;I;N$(I):PRINT @18,3+I;RI(I);" ca":NEXT
5710 PRINT @4,12;"Votre Bourse:";RI(P)"Cerfs Ag"
5720 S$="COMBIEN de C.Ag ":GOSUB 5650:PRINT @3,14;S$;
5730 INPUT DO:IF DO> RI(P) THEN PING:GOTO 5700
5735 IF DO=0 THEN 5790
5740 PRINT@4,16;CHR$(145);"ENRICHIR QUEL HEROS ?  0:Aucun ";CHR$(144);
5750 GET A$:A=VAL(A$):IF A>6 THEN PING:GOTO 5750
5760 IF A=0 THEN 5890
5770 RI(A)=RI(A)+DO:RI(P)=RI(P)-DO:PING:GOTO 5700
5790 RETURN

5800 S$="Quel Objet ? 0:aucun ":GOSUB 5600:PRINT @13,24;S$
5810 GET A$:O=VAL(A$):IF O>OP(P) THEN PING:GOTO 5810
5820 IF O=0 THEN 5920
5822 IT=SAD(P,O):IF (IT>21 AND IT<31) OR IT>38 THEN L=24:GOSUB 5950:GOTO 5800
5830 L=14:CLS:S$=N$(P)+" DONNE":GOSUB 10000
5840 S$=IT$(SAD(P,O)):GOSUB 5600:PRINT@8,3;S$
5850 FORI=1TO6:PRINT @12,4+I;I;N$(I):NEXT
5860 PRINT@14,12;CHR$(145);" A QUEL HEROS ?  ";CHR$(144);
5870 GET A$:A=VAL(A$):IF A>6 OR A=P THEN PING:GOTO 5870
5880 IF SAD(A,6)>0 THEN L=12:GOSUB 5950:GOTO 5860
5882 I=0 
5885 REPEAT:I=I+1
5900 UNTIL SAD(A,I)=0
5910 SAD(A,I)=SAD(P,O):SAD(P,O)=0
5915 CH=O:GOSUB 8920:GOTO 4620
5920 RETURN
5950 PING:PRINT @16,L;"  !IMPOSSIBLE!  ":WAIT150: RETURN

7200 REM SAC
7210 IF SAD(P,1)=0 THEN I=1:GOTO 7260
7220 I=0:REPEAT 
7230 I=I+1
7240 UNTIL SAD(P,I)=0 OR I=6
7250 IF SAD(P,6)>0 THEN M$="SAC PLEIN!":GOTO 9000
7260 SAD(P,I)=SS+CH
7270 RETURN


8920 FOR I=CHTO5
8930 IF SAD(P,I+1)>0 THEN SAD(P,I)=SAD(P,I+1):SAD(P,I+1)=0
8940 NEXTI 
8950 RETURN

8960 REM VALIDE
8970 RI(P)=RI(P)-PRIX%(CH+SS):FD=148:GOTO 9000
8980 GOTO 6130

8990 M$="Impossible pour un "+C$(CP(P))

9000 T=INT((38-LEN(M$))/2)
9010 PING:CLS:PRINT:PRINT@T,5;CHR$(FD);" ";M$;" ";CHR$(144)
9020 WAIT140:SS=0:GOTO 6130

10000 TEXT:PAPER0:INK EE(VIL):PRINT
10020 PRINT" ************************************"
10030 FORI=1TOL:PRINT@2,I;"*":PRINT@38,I;"*":NEXTI
10040 T=INT((31-LEN(S$))/2)
10060 S$="< "+S$+" > ":GOSUB 5650:PRINT @ T,1;S$
10080 PRINT@2,I;"*************************************"
10100 RETURN

10800 L=19:PRINT@1,L;CHR$(145)"PERSONNAGES    CASTE      PV  ET  CA"
10820 FOR I=1TO6:L=L+1
10830  IF CP(I)=1 THEN ENC=131
10835  IF CP(I)=2 THEN ENC=135
10840  IF CP(I)=3 THEN ENC=134
10850  IF CP(I)=4 THEN ENC=133
10860  IF CP(I)=5 THEN ENC=130
10870  IF CP(I)=6 THEN ENC=132
10875  IF OK(I)=4 THEN ENC=129
10880  IF OK(I)>1 THEN S$=OK$(OK(I)) ELSE S$=C$(CP(I))
10890 PRINT @ 1,L;CHR$(ENC);I;N$(I);:PRINT @ 17,L;S$;
10900 PRINT @ 27,L;PV(I):PRINT @ 34-LEN(STR$(ET(I))),L;ET(I):PRINT @ 38-LEN(STR$(CA(I))),L;CA(I)
10910 NEXT I
10920 ENC=4
10950 RETURN

21000 REM CAMP : REM NEW ++++ 
21010 TEXT:CLS:POKE#26A,PEEK(#26A) AND 254:ENC=4:S$=" ++ CAMPEMENT ++ ":L=16
21020 GOSUB 10000:GOSUB 10800
21050 PRINT @8,5;" 1.Inspecter un heros"
21060 PRINT @8,7;" 2.Inspecter l'equipe": REM NEW ++++
21070 IF CA>19 AND CA<25 THEN PRINT @8,9;" 3.Ouvrir un coffre": REM NEW ++++
21080 PRINT @8,13;" 4.Dormir et Decamper " : REM NEW ++++
21100 GETA$:CH=VAL(A$):IF CH<1ORCH>4 OR (CH=3 AND CA=0)THEN 21100 : REM NEW ++++
21200 ON CH GOSUB 4400,5300,21300,21500: REM NEW ++++
21210 IF CH=4 THEN 300: REM NEW ++++
21250 GOTO 21000

21300 IF CA=0 THEN GOTO 21480
21305 CLS:S$="Voila un coffre, Qui l'ouvre ? ":GOSUB 5650
21310 PRINT @3,7;S$:GOSUB 10800
21320 GOSUB 30200:P=A:T1=T1+5
21330 IF CP(P)<>3 THEN PRINT @8,10;"C EST PAS SON TRUC !":ZAP:GOTO 21480: REM NEW ++++
21340 I=0
21350 REPEAT:I=I+1
21370 IF SAD(P,I)=39 THEN DFF=30 ELSE DFF=-20
21380 UNTIL I=6 OR SAD(P,I)=39
21390 SS=FNA(100):IF SS>AG(P)+DFF+T1 THEN PRINT @10,11;SS;"ET NON ! T1:";T1:ZAP:GOTO 21480: REM NEW ++++
21400 PRINT @10,10;"OUVERT ! BRAVO":PING
21405 IF CA=21 THEN CL(VIL,1)=1:S$="Un passe":SS=0:GOTO 21425
21410 SS=FNA(100): IF SS<5 OR SS>95 THEN SS=FNA(5)+48:GOTO 21420
21415 SS=FNA(22)+19:IF SS=40 OR (SS>21 AND SS<29) THEN 21415
21420 S$=IT$(SS)
21425 PRINT @5,12;"IL TROUVE: ";S$
21430 IF SAD(P,6)>0 THEN ZAP:PRINT @10,14;"PLEIN ! DOMMAGE":GOTO 21480
21435 I=0:J=0:NP=NP+1: REM NEW ++++
21440 REPEAT:I=I+1
21445 IF SAD(P,I)=0 THEN SAD(P,I)=SS:J=1
21450 UNTIL J=1 
21455 CA=0:CF=0: REM NEW ++++
21480 RETURN

21500 CLS:PRINT @10,8;" UNE PETITE SIESTE "
21510 GOSUB 22000:PRINT @7,10;" VOUS RECUPEREZ VOS SORTS ":WAIT TI*5
21580 PRINT @10,12;"  BON VOYAGE  ":WAIT TI*5
21590 RETURN

22000  FOR P=1TO6
22010  IF ET(P)>0 AND ET(P)<5 THEN ET(P)=ET(P)+FNA(3)
22020  IF CP(P)<4 THEN 22070
22030     FOR I=1TONI(P)
22040     SN(P,I)=FNA(2)+2
22050     NEXT I
22070 NEXT P
22100 RETURN

30000 S$="< ESPACE > ":GOSUB 5650:PRINT@13,L;S$:GETA$:IF A$<>" " THEN 30000
30001 RETURN

30100 IF KEY$="" THEN 30100
30101 RETURN

30200 GET A$:A=VAL(A$):IF A<1 OR A>6 THEN PING:GOTO 30200
30220 RETURN


48000 REM LOAD"TEAM.BIN"
48010 O1=#A000
48011 O1=O1+1:VIL=PEEK(O1)
48012 PRINT"Ville " VIL : IF VIL<>32 THEN 48020 
48013 O1=O1+1:X=PEEK(O1)
48014 O1=O1+1:Y=PEEK(O1)
48015 O1=O1+1:S=PEEK(O1)
48016 O1=O1+1:CA=PEEK(O1)
48017 O1=O1+1:VIL=PEEK(O1)
48018 PRINT"Ville " VIL "X " X "Y " Y "S " S "CA " CA
48020 FOR P=1TO6
48030 O1=O1+1:DD=PEEK(O1)
48040 FORJ=1TODD:O1=O1+1:N$(P)=N$(P)+CHR$(PEEK(O1)):NEXTJ
48050 O1=O1+1:RI(P)=DEEK(O1)*10:O1=O1+2
48055 CP(P)=PEEK(O1)
48056 O1=O1+1:MP(P)=PEEK(O1)
48060 O1=O1+1:CC(P)=PEEK(O1)
48070 O1=O1+1:CT(P)=PEEK(O1)
48080 O1=O1+1:FO(P)=PEEK(O1)
48100 O1=O1+1:AG(P)=PEEK(O1)
48110 O1=O1+1:IN(P)=PEEK(O1)
48120 O1=O1+1:FM(P)=PEEK(O1)
48130 O1=O1+1:PV(P)=PEEK(O1)
48140 O1=O1+1:ET(P)=PEEK(O1)
48150 O1=O1+1:OK(P)=PEEK(O1)
48160 O1=O1+1:NI(P)=PEEK(O1)
48170 O1=O1+1:XP(P)=DEEK(O1)
48180 O1=O1+2:WR(P)=PEEK(O1)
48190 O1=O1+1:WL(P)=PEEK(O1)
48200 O1=O1+1:PT(P)=PEEK(O1)
48210 O1=O1+1:CA(P)=PEEK(O1)
48220 O1=O1+1:BT(P)=PEEK(O1)
48230 FORI=1TO6:O1=O1+1:SAD(P,I)=PEEK(O1):NEXTI
48235 IF CP(P)>3 THEN FORI=1TO8:O1=O1+1:SN(P,I)=PEEK(O1):NEXT
48240 NEXT P
48250 O1=O1+1:BS=PEEK(O1)
48260 O1=O1+1:FI=PEEK(O1)
48265 O1=O1+1:SD=PEEK(O1)
48270 FOR L=1TO8:FOR C=1TO4:O1=O1+1:CLEF(L,C)=PEEK(O1):NEXT C,L
48280 FOR V=1TO8:FORM=1TO3:O1=O1+1:MISS(V,M)=PEEK(O1):NEXT M,V
48290 FOR I=1TO6:O1=O1+1:IG(I)=PEEK(O1):NEXT
48295 O1=O1+1:CO=PEEK(O1):PRINT "COMBATS";CO;O1;(O1-#A000)
48296 FOR V=1TO8:FORM=1TO5:O1=O1+1:TC(V,M)=PEEK(O1):NEXT M,V
48300 RETURN

49000 CLS:PRINT @ 8,12;CHR$(148);CHR$(131);"++ RETOUR LABYRINTHE ++ ";CHR$(144)
49010 O1=#A000
49011 O1=O1+1:POKEO1,32
49012 O1=O1+1:POKEO1,X
49013 O1=O1+1:POKEO1,Y
49014 O1=O1+1:POKEO1,S
49015 O1=O1+1:POKEO1,CA
49020 O1=O1+1:POKEO1,VIL
49030 FOR P=1TO6
49040 O1=O1+1:POKEO1,LEN(N$(P))
49050 FORJ=1TOLEN(N$(P))
49060 O1=O1+1:POKEO1,ASC(MID$(N$(P),J,1))
49070 NEXTJ
49080 O1=O1+1:DOKEO1,INT(RI(P)/10)
49090 O1=O1+2:POKEO1,CP(P)
49100 O1=O1+1:POKEO1,MP(P)
49100 O1=O1+1:POKEO1,CC(P)
49110 O1=O1+1:POKEO1,CT(P)
49120 O1=O1+1:POKEO1,FO(P)
49140 O1=O1+1:POKEO1,AG(P)
49150 O1=O1+1:POKEO1,IN(P)
49160 O1=O1+1:POKEO1,FM(P)
49180 O1=O1+1:POKEO1,PV(P)
49190 O1=O1+1:POKEO1,ET(P)
49200 O1=O1+1:POKEO1,OK(P)
49210 O1=O1+1:POKEO1,NI(P)
49215 O1=O1+1:DOKEO1,XP(P)
49220 O1=O1+2:POKEO1,WR(P)
49230 O1=O1+1:POKEO1,WL(P)
49240 O1=O1+1:POKEO1,PT(P)
49250 O1=O1+1:POKEO1,CA(P)
49260 O1=O1+1:POKEO1,BT(P)
49270 FORI=1TO6:O1=O1+1:POKEO1,SAD(P,I):NEXTI
49280 IF CP(P)>3 THEN FORI=1TO8:O1=O1+1:POKEO1,SN(P,I):NEXT
49290 NEXT P
49300 O1=O1+1:POKEO1,BS
49310 O1=O1+1:POKEO1,FI
49315 O1=O1+1:POKEO1,SD
49320 FOR L=1TO8:FOR C=1TO4:O1=O1+1:POKEO1,CLEF(L,C):NEXT C,L
49330 FOR V=1TO8:FORM=1TO3:O1=O1+1:POKEO1,MISS(V,M):NEXT M,V
49340 FORI=1TO6:O1=O1+1:POKEO1,IG(I):NEXT
49345 O1=O1+1:POKEO1,CO:PRINT "COMBATS";CO;O1;(O1-#A000)
49350 FOR V=1TO8:FORM=1TO5:O1=O1+1:POKEO1,TC(V,M):NEXT M,V
49390 PING:SAVEU "TEAM.BIN",A#A000,EO1
49395 REM SAVEU "TEAM2.BIN",A#A000,EO1
49400 RETURN

50000 TIME=20:HV=6:DFF=0
50010 DIM IT$(55), PR%(55), TP$(3,26), IP$(8,4)
50020 GOSUB 53100
50030 REM RECALL PR%,"T-PRIX",S
50050 REM RECALL IP$,"T-IMG-P",S
50060 FOR I=1TO4:READ TX$(I):NEXT I
50070 FOR I=1TO5:FOR J=1TO4:READ PS$(I,J):NEXT J,I
50080 FOR I=1TO4:READ OK$(I):NEXTI
50090 DEF FNA(X)=INT(RND(1)*X)+1
50100 FORI=1TO6:READ C$(I):NEXTI
50110 FORI=1TO8:READ M$(I),CR$(I),EENCRE(I):NEXTI
50120 NS=4:NI=0:FOR I=1TO4:READ SHOP$(I),N(I),CO(I):NI=NI+N(I):NEXT I
50130 FORI=1TO6:READ IG$(I):NEXT
50140 FORH=1TO3:FORI=1TO8:READ SPELL$(H,I):NEXTI,H

50400 RESTORE:RETURN

51010 DATA "Ouille!","Le mur n'a rien senti","Tu as bu ?","Ou est la clef ?"
51021 DATA "Ld STARK"," MESTRE "," PRISON ","CELLIERS "
51022 DATA "Ly TULLY"," GEOLIER","SKY CELL"," COFFRES "
51023 DATA " TYRION "," JAIME  "," PRISON ","CHAPELLE "
51024 DATA " EDMURE "," HOSTER ","*HILTON*","CELLIERS "
51025 DATA "  LORAS ","MAERGERY"," PRISON "," COFFRES "
51030 DATA OK, "-Empoi- ", "-Paral- ", ">MORT< "

51050 DATA Chevalier,Paladin,Ranger,Sorcier,Mestre,Septon
51060 DATA STARK,WINTERFELL,6, ARRYN,VAL D'ARRYN,7, LANNISTER,CASTRAL ROC,1
51070 DATA TULLY,CONFLANS,2, TYRELL,Le BIEF,3, BARATHEON,L'ORAGE,4, MARTELL,DORNE,5
51080 DATA AUCUNE,KING'S LANDING,6

51100 DATA ARMURERIE,19,7, HERBORISTERIE,11,2, BAZAR,11,3, ANIMALERIE,7,5

52000 DATA "raisin de Dorne", "poudre d os de dragon", "venin de vipere", "huile d Arryn", "sang royal", "cheveu Targaryen"

53000 DATA SOMMEIL, FEU, PIERRE, VENIN, SANG,  FOUDRE, LAVE, SEISME
53010 DATA EAU, SERUM, MUSCLE, BOUCLIER, ELIXIR, ECRAN, VIE, MORT
53020 DATA EPEE-FEU, FORCE, CHARME, VISION, GLACE, ILLUSION, VENT, DRAGONS

53090 REM Lecture TITEMS
53100 TEXT:CLS:PRINT @ 8,12;CHR$(145);CHR$(135);"++ VEUILLEZ PATIENTER ++ ";CHR$(144):LOAD "TITEMS.BIN"
53110 O1=#A000
53120 LI=PEEK(O1)
53130 FOR I=1 TO LI
53140   O1=O1+1:LG=PEEK(O1)
53150   S$=""
53160   IF LG=0 THEN 53200
53170   FOR J=1 TO LG
53180     O1=O1+1:S$=S$+CHR$(PEEK(O1))
53190   NEXT
53200   ITEM$(I)=S$
53210 NEXT
53220 RETURN
